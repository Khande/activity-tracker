buildscript {
	ext.kotlin_version = '1.0.0'
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.3"
	}
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'

ext.home = System.env.HOME
ext.ideaPath = "/Applications/IntelliJ IDEA 2016.app/Contents"

repositories {
	mavenCentral()
	mavenLocal() // this is to pick up liveplugin jar (assuming you have it checked out and can do 'gradle install')
	maven { url 'lib' }
}
enableProvidedScope()

dependencies {
	compile "org.codehaus.groovy:groovy:2.4.6"
	compile "joda-time:joda-time:2.9.2"
	compile "org.apache.commons:commons-csv:1.3"
	compile "liveplugin:live-plugin:0.5.10 beta"

	// dependencies below are "provided" because IJ should already have them on classpath
	provided "org.jetbrains.kotlin:kotlin-stdlib:1.0.3"
	provided "org.jetbrains.kotlin:kotlin-test-junit:1.0.3"
	provided "junit:junit:4.11" // also includes hamcrest-core
	provided fileTree(dir: "${ideaPath}/lib/", include: "*.jar")
	provided fileTree(dir: "${ideaPath}/plugins/tasks/lib/", include: "*.jar") // required for tracking current task
}

sourceSets {
	main {
		kotlin { srcDir "./src/main" }
		resources { srcDir "./src/resources" }
	}
	test {
		kotlin { srcDir "./src/test" }
	}
}

task downloadDependenciesToLibFolder(type: Copy) {
	from configurations.runtime
	into './lib'
}

// TODO add task for zipping plugin with all dependencies (to replace IJ Project Settings -> Artifacts)

def enableProvidedScope() {
	configurations {
		provided
		compile.extendsFrom provided
	}
	idea {
		module { scopes.PROVIDED.plus += [configurations.provided] }
	}
}

